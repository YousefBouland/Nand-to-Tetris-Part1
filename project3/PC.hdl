// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16], load, inc, reset;
    OUT out[16];

    PARTS:
    Inc16(in=altout, out=altoutp1);
    Mux16(a= altout, b= altoutp1, sel=inc, out=junct0);
    Mux16(a=junct0, b=in, sel=load, out=junct1);
    Mux16(a=junct1, b=false, sel=reset, out=junct2);
    Register(in=junct2, load=true, out=altout, out=out);
}